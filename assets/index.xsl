<?xml version="1.0" encoding="UTF-8"?>
<!--
(The MIT License)

Copyright (c) 2022 Yegor Bugayenko

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the 'Software'), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED 'AS IS', WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
-->
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="2.0">
  <xsl:output encoding="UTF-8" method="html"/>
  <xsl:template match="/">
    <html>
      <head>
        <meta http-equiv="Content-Type" content="text/html; charset=UTF-8"/>
        <title>cobench</title>
        <meta charset="UTF-8"/>
        <meta content="width=device-width, initial-scale=1.0" name="viewport"/>
        <link rel="icon" href="https://raw.githubusercontent.com/yegor256/cobench/master/logo.svg" type="image/svg"/>
        <link href="https://cdn.jsdelivr.net/gh/yegor256/tacit@gh-pages/tacit-css.min.css" rel="stylesheet"/>
        <link href="https://cdn.jsdelivr.net/gh/yegor256/drops@gh-pages/drops.min.css" rel="stylesheet"/>
        <script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/jquery/3.6.0/jquery.min.js"/>
        <script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/jquery.tablesorter/2.31.3/js/jquery.tablesorter.min.js"/>
        <script type="text/javascript">
          $(function() {
            $("#metrics").tablesorter();
          });
        </script>
        <style>
          td, th { font-family: monospace; font-size: 18px; }
          .num { text-align: right; }
          .left { border-bottom: 0; }
          header { text-align: center; }
          footer { text-align: center; font-size: 0.8em; }
          article { width: 60em; border: 0; }
          .sorter { cursor: pointer; }
        </style>
      </head>
      <body>
        <section>
          <header>
            <p>
              <img src="https://raw.githubusercontent.com/yegor256/cobench/master/logo.svg" style="width:64px"/>
            </p>
          </header>
          <article>
            <table id="metrics">
              <xsl:apply-templates select="cobench/titles"/>
              <xsl:apply-templates select="cobench/coders"/>
            </table>
          </article>
          <footer>
            <p>
              <xsl:text>The page was generated by </xsl:text>
              <a href="https://github.com/yegor256/cobench">
                <xsl:text>cobench</xsl:text>
              </a>
              <xsl:text> on </xsl:text>
              <xsl:value-of select="cobench/@time"/>
              <xsl:text>.</xsl:text>
            </p>
            <p>
              <xsl:text>The numbers you see reflect the activity of the last </xsl:text>
              <b>
                <xsl:value-of select="cobench/@days"/>
                <xsl:text> days</xsl:text>
              </b>
              <xsl:text>.</xsl:text>
            </p>
          </footer>
        </section>
      </body>
    </html>
  </xsl:template>
  <xsl:template match="cobench/titles">
    <thead>
      <tr>
        <th/>
        <xsl:for-each select="title">
          <xsl:sort select="."/>
          <th class="sorter num">
            <xsl:value-of select="."/>
          </th>
        </xsl:for-each>
      </tr>
    </thead>
  </xsl:template>
  <xsl:template match="cobench/coders">
    <tbody>
      <xsl:apply-templates select="coder"/>
    </tbody>
  </xsl:template>
  <xsl:template match="coder">
    <tr>
      <td>
        <a href="https://github.com/{@id}">
          <xsl:text>@</xsl:text>
          <xsl:value-of select="@id"/>
        </a>
      </td>
      <xsl:for-each select="metrics/m">
        <xsl:sort select="@id"/>
        <xsl:apply-templates select="."/>
      </xsl:for-each>
    </tr>
  </xsl:template>
  <xsl:template match="m">
    <td class="num">
      <xsl:choose>
        <xsl:when test="@href">
          <a href="{@href}">
            <xsl:value-of select="."/>
          </a>
        </xsl:when>
        <xsl:otherwise>
          <xsl:value-of select="."/>
        </xsl:otherwise>
      </xsl:choose>
    </td>
  </xsl:template>
  <xsl:template match="node()|@*">
    <xsl:copy>
      <xsl:apply-templates select="node()|@*"/>
    </xsl:copy>
  </xsl:template>
</xsl:stylesheet>
